# Voyager SDK Example showing use of AxInferenceNet with OpenCV
# Copyright Axelera AI, 2025
cmake_minimum_required(VERSION 3.12)
project(axinferencenet_example LANGUAGES CXX VERSION 0.8)
set(CMAKE_CXX_STANDARD 20)
set(CMAKE_POSITION_INDEPENDENT_CODE ON)

find_package(PkgConfig REQUIRED)

pkg_check_modules(axruntime REQUIRED axruntime)
pkg_check_modules(opencv4 REQUIRED opencv4)
pkg_check_modules(axstreamer REQUIRED axstreamer)

# Main example
add_executable(axinferencenet_example axinferencenet_example.cpp)

# Tensor output example
add_executable(axinferencenet_tensor axinferencenet_tensor.cpp)

# Cascaded example
add_executable(axinferencenet_cascaded axinferencenet_cascaded.cpp)

foreach(target axinferencenet_example axinferencenet_tensor axinferencenet_cascaded)
    target_compile_options(${target} PRIVATE
        -Wall
        -Werror
        -Wno-sign-compare
        -Wno-unused-variable
        $<$<COMPILE_LANGUAGE:CXX>:-Wno-deprecated-enum-enum-conversion>
        $<$<COMPILE_LANGUAGE:CXX>:-Wno-deprecated-declarations>
    )
    target_include_directories(${target} PRIVATE
        ${axruntime_INCLUDE_DIRS}/include
        ${opencv4_INCLUDE_DIRS}
        ${axstreamer_INCLUDE_DIRS}
    )
    target_link_libraries(${target} PUBLIC
        ${axruntime_LIBRARIES}
        ${opencv4_LIBRARIES}
        ${axstreamer_LIBRARIES}
    )
    target_link_directories(${target} PUBLIC
        ${axruntime_LIBRARY_DIRS}
        ${opencv4_LIBRARY_DIRS}
        ${axstreamer_LIBRARY_DIRS}
    )
    install(TARGETS ${target} RUNTIME DESTINATION bin)
endforeach()
